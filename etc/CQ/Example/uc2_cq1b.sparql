#UC2-CQ 1b: In which Stock Keeping Units is substance <$Substance> used?

PREFIX idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:        <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cmns-prd: 	<https://www.omg.org/spec/Commons/ProductsAndServices/>
PREFIX cmns-dsg:   <https://www.omg.org/spec/Commons/Designators/>
PREFIX cmns-col: <https://www.omg.org/spec/Commons/Collections/>
PREFIX cmns-cmp: <https://www.omg.org/spec/Commons/Composites/>
PREFIX cmns-rlcmp:  <https://www.omg.org/spec/Commons/RolesAndCompositions/>
PREFIX idmp-mprd: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
PREFIX cmns-txt:    <https://www.omg.org/spec/Commons/TextDatatype/>

SELECT DISTINCT ?StockKeepingUnit (SAMPLE(?StockKeepingUnitLabel) AS ?StockKeepingUnitName)

where {
        

      # Bind Variable substance <$Substance>
	  # Example for AMPLODIPINE: <https://spec.pistoiaalliance.org/idmp/ontology/EXT/Examples/AmlodipineExample/Amlodipine>
	  BIND( <https://spor.ema.europa.eu/v2/SubstanceDefinition/100000085259> AS $Substance )
        
        #Get the PharmaceuticalProduct which is connected to SKU via PackagedMedicinalProduct
		
  		?StockKeepingUnit a cmns-prd:StockKeepingUnit ; 
			cmns-dsg:denotes ?PackagedMedicinalProduct. 
			
		# Using property path as PharmaceuticalProduct can be contained in multilevel packaging of PackagedMedicinalProduct
		# idmp-mprd:contains+ could be replaced by idmp-mprd:contains{1,n} where n is number of level of packaging for PharmaceuticalProduct
		
        #?PackagedMedicinalProduct idmp-mprd:contains+ ?PharmaceuticalProduct . 
        ?PackagedMedicinalProduct cmns-col:comprises+/idmp-mprd:contains+ ?PharmaceuticalProduct . 
         {
		   #Case1 : 	
           # PharmaceuticalProduct is comprised of the substance
		   
           ?PharmaceuticalProduct cmns-col:comprises $Substance       
  
         } 
         UNION 
         {
			
            #Get the ActiveIngredient which is connected to PharmaceuticalProduct via PharmaceuticalProductComposition
			
            $PharmaceuticalProductComposition cmns-dsg:defines $PharmaceuticalProduct .
         	$PharmaceuticalProductComposition idmp-mprd:hasActiveIngredient ?ActiveIngredient.
			{
				#Case 2:	
				#substance is an active moiety for a substance connected to the pharmaceutical product via ActiveIngredient
				
				?ActiveIngredient cmns-rlcmp:isPlayedBy ?ActiveMoiety.
				?ActiveMoiety ^idmp-sub:isActiveMoietyOf|idmp-sub:hasActiveMoiety $Substance .
			
			}
			UNION
			{
				#Case3 :
				#substance is connected to the pharmaceutical product via ActiveIngredient
				
				?ActiveIngredient cmns-rlcmp:isPlayedBy $Substance.
				
			}

        }
        
		# Make sure that we only return actual substances
        ?StockKeepingUnit a/rdfs:subClassOf* cmns-prd:StockKeepingUnit .

		# Optionally, get the name of the stock keeping unit
		OPTIONAL{?StockKeepingUnit  cmns-txt:hasTextValue  ?StockKeepingUnitLabel }

		
} GROUP BY ?StockKeepingUnit
